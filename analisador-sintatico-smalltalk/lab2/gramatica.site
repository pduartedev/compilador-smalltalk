Crate -> Item_List

Item_List -> Item Item_List
Item_List -> ''

Item -> Class_Definition
Item -> Statement TOKEN_period

Class_Definition -> TOKEN_identifier TOKEN_subclass TOKEN_identifier TOKEN_left_bracket Class_Body TOKEN_right_bracket

Class_Body -> Temporaries Method_Definitions
Class_Body -> Method_Definitions

Temporaries -> TOKEN_pipe TOKEN_identifier Variable_List TOKEN_pipe

Variable_List -> TOKEN_identifier Variable_List
Variable_List -> ''

Method_Definitions -> Method_Definition Method_Definitions
Method_Definitions -> ''


Method_Definition -> Message_Pattern TOKEN_left_bracket Temporaries_Opt Statement_Opt TOKEN_right_bracket

Message_Pattern -> Unary_Pattern
Message_Pattern -> Binary_Pattern
Message_Pattern -> Keyword_Pattern

Unary_Pattern -> TOKEN_identifier

Binary_Pattern -> TOKEN_binary_selector TOKEN_identifier

Keyword_Pattern -> Keyword_Pattern_Element Keyword_Pattern_Element_List

Keyword_Pattern_Element -> TOKEN_keyword TOKEN_identifier

Keyword_Pattern_Element_List -> Keyword_Pattern_Element Keyword_Pattern_Element_List
Keyword_Pattern_Element_List -> ''

Temporaries_Opt -> Temporaries
Temporaries_Opt -> ''

Statement_Opt -> Statements
Statement_Opt -> ''


Statements -> Statement Statement_List

Statement -> Return_Statement
Statement -> Expression

Statement_List -> TOKEN_period
Statement_List -> TOKEN_period Statement Statement_List
Statement_List -> ''

Return_Statement -> TOKEN_return_operator Expression


Expression -> Assignment
Expression -> Basic_Expression

Assignment -> Assignment_Target TOKEN_assignment_operator Expression

Assignment_Target -> TOKEN_identifier

Basic_Expression -> Primary Message_Sequence

Primary -> TOKEN_identifier
Primary -> Literal
Primary -> TOKEN_left_paren Expression TOKEN_right_paren
Primary -> Block_Constructor
Primary -> TOKEN_self
Primary -> TOKEN_super
Primary -> TOKEN_true
Primary -> TOKEN_false
Primary -> TOKEN_nil

Message_Sequence -> Message_Chain Cascaded_Messages
Message_Sequence -> ''


Message_Chain -> Unary_Message_Chain
Message_Chain -> Binary_Message_Chain
Message_Chain -> Keyword_Message

Unary_Message_Chain -> Unary_Message_List
Unary_Message_Chain -> Unary_Message_List Binary_Message_Chain
Unary_Message_Chain -> Unary_Message_List Keyword_Message

Binary_Message_Chain -> Binary_Message_List
Binary_Message_Chain -> Binary_Message_List Keyword_Message

Unary_Message_List -> Unary_Message Unary_Message_List
Unary_Message_List -> Unary_Message

Unary_Message -> TOKEN_identifier

Binary_Message_List -> Binary_Message Binary_Message_List
Binary_Message_List -> Binary_Message

Binary_Message -> Binary_Selector Binary_Argument

Binary_Selector -> TOKEN_binary_selector

Binary_Argument -> Primary Unary_Message_List
Binary_Argument -> Primary

Keyword_Message -> Keyword_Message_Element Keyword_Message_Element_List

Keyword_Message_Element -> TOKEN_keyword Keyword_Argument

Keyword_Message_Element_List -> Keyword_Message_Element Keyword_Message_Element_List
Keyword_Message_Element_List -> ''

Keyword_Argument -> Primary Unary_Message_List Binary_Message_List
Keyword_Argument -> Primary Unary_Message_List
Keyword_Argument -> Primary Binary_Message_List
Keyword_Argument -> Primary

Cascaded_Messages -> TOKEN_semicolon Message_Chain Cascaded_Messages
Cascaded_Messages -> ''


Literal -> Number_Literal
Literal -> String_Literal
Literal -> Character_Literal
Literal -> Symbol_Literal
Literal -> Selector_Literal
Literal -> Array_Literal

Number_Literal -> TOKEN_decimal_integer
Number_Literal -> TOKEN_radix_integer
Number_Literal -> TOKEN_float
Number_Literal -> TOKEN_scaled_decimal

Character_Literal -> TOKEN_quoted_character

String_Literal -> TOKEN_quoted_string

Symbol_Literal -> TOKEN_hashed_string

Selector_Literal -> TOKEN_quoted_unary_selector
Selector_Literal -> TOKEN_quoted_binary_selector
Selector_Literal -> TOKEN_quoted_keyword_selector

Array_Literal -> TOKEN_array_begin Array_Element Array_Element_List TOKEN_right_paren

Array_Element_List -> Array_Element Array_Element_List
Array_Element_List -> ''

Array_Element -> Literal
Array_Element -> TOKEN_nil
Array_Element -> TOKEN_true
Array_Element -> TOKEN_false
Array_Element -> TOKEN_identifier


Block_Constructor -> TOKEN_left_bracket Block_Body_Content TOKEN_right_bracket

Block_Body_Content -> Temporaries_Opt Block_Statements_Opt

Block_Statements_Opt -> Block_Statements
Block_Statements_Opt -> ''

Block_Statements -> Statement Block_Statement_List

Block_Statement_List -> TOKEN_period
Block_Statement_List -> TOKEN_period Statement Block_Statement_List
Block_Statement_List -> ''